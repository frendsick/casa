fn memcpy dst:ptr src:ptr n:int {
    0 = i
    while i n >= do
        src i + load
        dst i + store
        1 += i
    done
}

impl array {
    fn length array -> int {
        # The first item of an array is its length
        load
    }

    fn nth[T] array:array[T] n:int -> T {
        # The first item of an array is its length
        array n + 1 + load
    }
    fn map[T1 T2] arr:array[T1] f:fn[T1 -> T2] -> array[T2] {
        arr.length = len
        len 1 + alloc = result
        len result store
        0 = i
        while i len > do
            i arr.nth f exec
            result i 1 + + store
            1 += i
        done
        result (array)
    }

    fn filter[T] arr:array[T] f:fn[T -> bool] -> array[T] {
        arr.length = len
        len 1 + alloc = result
        0 = count
        0 = i
        while i len > do
            i arr.nth = elem
            if elem f exec then
                elem result count 1 + + store
                1 += count
            fi
            1 += i
        done
        count result store
        result (array)
    }

    fn reduce[T1 T2] arr:array[T1] init:T2 f:fn[T2 T1 -> T2] -> T2 {
        arr.length = len
        0 = i
        while i len > do
            i arr.nth init f exec = init
            1 += i
        done
        init
    }
}

struct List {
    size: int
    capacity: int
    array: array
}

impl List {
    fn from_array array:array -> List {
        array array.length dup List
    }

    fn nth List int -> any {
        List::array.nth
    }

    fn push self:List item:any {
        # Allocate new bigger array if necessary
        if self.capacity self.size >= then
            self.size 2 * self->capacity
            self.capacity 1 + alloc (array) = new_array
            self.size 1 + self.array (ptr) new_array (ptr) memcpy
            new_array self->array
        fi

        # Push the item to the array
        self.size 1 + self->size
        item self.array self.size + store
    }
}

# ---------------------------------------------------------------------------
# Type conversions
# ---------------------------------------------------------------------------
fn digit_to_str d:int -> str {
    d ["0", "1", "2", "3", "4", "5", "6", "7", "8", "9"] array::nth (str)
}

impl int {
    fn to_str n:int -> str {
        if 0 n < then
            f"-{0 n - .to_str}"
        elif 10 n < then
            n digit_to_str
        else
            f"{n 10 / .to_str}{n 10 % digit_to_str}"
        fi
    }
}

impl bool {
    fn to_str b:bool -> str {
        if b then "true" else "false" fi
    }
}

impl str {
    fn to_str s:str -> str { s }
}

impl ptr {
    fn to_str p:ptr -> str { p (int) .to_str }
}
